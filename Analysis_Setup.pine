// @version=5
indicator("Analysis_Setup", overlay=true)

// Demark Pivots Function
Demark(open, high, low, close) =>
    x = 0.0
    if close < open
        x := high + (2*low) + close
    else if close > open
        x := (2*high) + low + close
    else
        x := high + low + (2*close)
    support = (x/2) - high
    resistance = (x/2) - low
    [support, resistance]

// Monthly Pivots
[mo, mh, ml, mc] = request.security(syminfo.tickerid, "M", [open[1], high[1], low[1], close[1]], lookahead=barmerge.lookahead_on)
[monthly_support, monthly_resistance] = Demark(mo, mh, ml, mc)

// Plot Monthly Pivots
plot(monthly_resistance, title="Monthly Resistance", color=color.fuchsia, linewidth=3)
plot(monthly_support, title="Monthly Support", color=color.aqua, linewidth=3)

// Yearly Pivots
[yo, yh, yl, yc] = request.security(syminfo.tickerid, "12M", [open[1], high[1], low[1], close[1]], lookahead=barmerge.lookahead_on)
[yearly_support, yearly_resistance] = Demark(yo, yh, yl, yc)

// Plot Yearly Pivots
plot(yearly_resistance, title="Yearly Resistance", color=color.red, linewidth=4)
plot(yearly_support, title="Yearly Support", color=color.green, linewidth=4)

// Security Function
RequestSecurity(tf, val) =>
    request.security(syminfo.tickerid, tf, val, lookahead=barmerge.lookahead_on)
    
// Moving Average
MA_Value = ta.sma(close, 44)

// Standard Deviation
dev = 2 * ta.stdev(close, 44)

// Daily Bands
MA_D = RequestSecurity("D", MA_Value)
dev_D = RequestSecurity("D", dev)
UB_D = MA_D + dev_D
LB_D = MA_D - dev_D

// Weekly Bands
MA_W = RequestSecurity("W", MA_Value)
dev_W = RequestSecurity("W", dev)
UB_W = MA_W + dev_W
LB_W = MA_W - dev_W

// Plot Daily Bands
plot(UB_D, "Daily Upper Band", color.blue, linewidth=2)
plot(MA_D, "Daily Base Band", color.black, linewidth=2)
plot(LB_D, "Daily Lower Band", color.blue, linewidth=2)

// Plot Weekly Bands
plot(UB_W, "Weekly Upper Band", color.blue, linewidth=4)
plot(MA_W, "Weekly Base Band", color.black, linewidth=4)
plot(LB_W, "Weekly Lower Band", color.blue, linewidth=4)
