//@version=5
indicator(title="Pivot Breakout Indicator", overlay=true)

// Demark Pivots
Demark(open, high, low, close) =>
    x = 0.0
    if close < open
        x := high + (2*low) + close
    else if close > open
        x := (2*high) + low + close
    else
        x := high + low + (2*close)
    support = (x/2) - high
    resistance = (x/2) - low
    [support, resistance]

// Daily Pivots
[do, dh, dl, dc] = request.security(syminfo.tickerid, "D", [open[1], high[1], low[1], close[1]], lookahead=barmerge.lookahead_on)
[daily_support, daily_resistance] = Demark(do, dh, dl, dc)
plot(daily_resistance, title="Daily Resistance", color=color.fuchsia, linewidth=3)
plot(daily_support, title="Daily Support", color=color.aqua, linewidth=3)

// Weekly Pivots
[wo, wh, wl, wc] = request.security(syminfo.tickerid, "W", [open[1], high[1], low[1], close[1]], lookahead=barmerge.lookahead_on)
[weekly_support, weekly_resistance] = Demark(wo, wh, wl, wc)
plot(weekly_resistance, title="Weekly Resistance", color=color.red, linewidth=4)
plot(weekly_support, title="Weekly Support", color=color.green, linewidth=4)

// Minor Trend
sma21 = ta.sma(close, 21)
dev21 = 2 * ta.stdev(close, 21)
upper21 = sma21 + dev21
lower21 = sma21 - dev21
plot(upper21, "Minor Trend UB", color=color.blue, linewidth=2)
plot(sma21, "Minor Trend MA", color=color.black, linewidth=2)
plot(lower21, "Minor Trend LB", color=color.blue, linewidth=2)

// Major Trend
ema44 = ta.ema(close, 44)
dev44 = 2 * ta.stdev(close, 44)
upper44 = ema44 + dev44
lower44 = ema44 - dev44
plot(upper44, "Major Trend UB", color=color.blue, linewidth=4)
plot(ema44, "Major Trend MA", color=color.black, linewidth=4)
plot(lower44, "Major Trend LB", color=color.blue, linewidth=4)
